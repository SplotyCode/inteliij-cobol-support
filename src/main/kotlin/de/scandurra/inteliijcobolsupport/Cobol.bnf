{
  parserClass="de.scandurra.inteliijcobolsupport.parser.CobolParser"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Cobol"
  psiImplClassSuffix="Impl"
  psiPackage="de.scandurra.inteliijcobolsupport.psi"
  psiImplPackage="de.scandurra.inteliijcobolsupport.psi.impl"

  elementTypeHolderClass="de.scandurra.inteliijcobolsupport.psi.CobolTypes"
  elementTypeClass="de.scandurra.inteliijcobolsupport.psi.CobolElementType"
  tokenTypeClass="de.scandurra.inteliijcobolsupport.psi.CobolTokenType"
}

/* this definition is not as strict as it could
 * it also causes the psi to be not as nested as it could be
 * I dont really like it but it is okayish for a small sample project */
cobolFile ::= line*

line ::= LINE_NUMBER lineBody

private lineBody ::=
    identificationDivision
  | programIdLine
  | dataDivision
  | workingStorageSection
  | dataDescription
  | procedureDivision
  | paragraphHeader
  | sentence
  ;

refIdent ::= IDENT { mixin="de.scandurra.inteliijcobolsupport.psi.CobolRefIdentMixin" }

identificationDivision ::= IDENTIFICATION DIVISION DOT
programIdLine          ::= PROGRAM_ID DOT? IDENT (DOT | sentence)?
dataDivision           ::= DATA DIVISION DOT
workingStorageSection  ::= WORKING_STORAGE SECTION DOT
procedureDivision      ::= PROCEDURE DIVISION DOT

paragraphHeader        ::= IDENT DOT

dataDescription        ::= NUMBER IDENT pictureClause valueClause? DOT
pictureClause          ::= (PIC | PICTURE) pictureString
pictureString          ::= NUMBER (LPAREN NUMBER RPAREN)?  /* todo */
valueClause            ::= VALUE NUMBER

sentence ::= statement_with_prefix+ LINE_NUMBER? DOT
private statement_with_prefix ::= LINE_NUMBER? statement

statement ::= performStmt | stopStmt | displayStmt

performStmt            ::= PERFORM refIdent performVarying?
performVarying         ::= VARYING refIdent FROM NUMBER BY NUMBER UNTIL condition
condition              ::= refIdent EQ NUMBER

stopStmt               ::= STOP RUN
displayStmt            ::= DISPLAY displayArg+
displayArg             ::= STRING | refIdent
